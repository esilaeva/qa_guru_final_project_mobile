plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'guru.qa'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def selenideVersion = "6.13.0",
    appiumVersion = "8.3.0",
    allureVersion = "2.21.0",
    slf4jVersion = "2.0.7",
    junitVersion = "5.9.3",
    restAssuredVersion = "5.3.0",
    ownerVersion = "1.0.4"


allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation (
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:$appiumVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "commons-io:commons-io:2.11.0",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion"
    )
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task negative_test(type: Test) {
    useJUnitPlatform {
        includeTags("negative")
    }
}

task positive_test(type: Test) {
    useJUnitPlatform {
        includeTags("positive")
    }
}